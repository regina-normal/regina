# Engine

SET ( SOURCES engine shareableobject )
# Each of these subdirectories modifies the SOURCE variable,
# adding more source files
ADD_SUBDIRECTORY("algebra")
ADD_SUBDIRECTORY("angle")
ADD_SUBDIRECTORY("census")
ADD_SUBDIRECTORY("data")
ADD_SUBDIRECTORY("enumerate")
ADD_SUBDIRECTORY("file")
ADD_SUBDIRECTORY("foreign")
ADD_SUBDIRECTORY("manifold")
ADD_SUBDIRECTORY("maths")
ADD_SUBDIRECTORY("packet")
ADD_SUBDIRECTORY("progress")
ADD_SUBDIRECTORY("snappea")
ADD_SUBDIRECTORY("split")
ADD_SUBDIRECTORY("subcomplex")
ADD_SUBDIRECTORY("surfaces")
ADD_SUBDIRECTORY("triangulation")
ADD_SUBDIRECTORY("utilities")

ADD_DEFINITIONS(-DREGINA_DATADIR=\"${PKGDATADIR}\")
ADD_DEFINITIONS(-DREGINA_PYLIBDIR=\"${PKGLIBDIR}/python\")
IF(ICONV_INCLUDE_DIR)
  ADD_DEFINITIONS(-DICONV_H=\"${ICONV_INCLUDE_DIR}/iconv.h\")
ENDIF(ICONV_INCLUDE_DIR)


INCLUDE_DIRECTORIES(${CMAKE_SOURCE_DIR}/engine)
INCLUDE_DIRECTORIES(${CMAKE_BINARY_DIR}/engine)
ADD_LIBRARY("regina-engine" SHARED ${SOURCES} )
SET(ENGINE_LINK_LIBRARIES ${LIBXML2_LIBRARIES} ${GMP_LIBRARIES} ${ZLIB_LIBRARIES} ${CMAKE_THREAD_LIBS_INIT})
TARGET_LINK_LIBRARIES(regina-engine ${ENGINE_LINK_LIBRARIES} ${ICONV_LIBRARY})

SET( HEADERS ${HEADERS} docs.h engine.h shareableobject.h )
SET( MANS ${MANS} regina-engine-config.1 )

# Build space-separated flag lists for regine-engine-config:
GET_DIRECTORY_PROPERTY(ENGINE_INCLUDE_DIRECTORIES DIRECTORY .. INCLUDE_DIRECTORIES)
foreach(arg ${ENGINE_INCLUDE_DIRECTORIES})
  SET(RECONFIG_INCLUDE_FLAGS "${RECONFIG_INCLUDE_FLAGS} -I${arg}")
endforeach(arg ${ENGINE_INCLUDE_DIRECTORIES})
foreach(arg ${ENGINE_LINK_LIBRARIES})
  SET(RECONFIG_LINK_FLAGS "${RECONFIG_LINK_FLAGS} ${arg}")
endforeach(arg ${ENGINE_LINK_LIBRARIES})
CONFIGURE_FILE (
  regina-engine-config.in
  "${PROJECT_BINARY_DIR}/engine/regina-engine-config"
  @ONLY
)

INSTALL(FILES ${HEADERS} DESTINATION ${INCLUDEDIR})
INSTALL(TARGETS regina-engine LIBRARY DESTINATION lib)
INSTALL(PROGRAMS ${PROJECT_BINARY_DIR}/engine/regina-engine-config DESTINATION bin)
INSTALL(FILES regina-engine-config.1 DESTINATION ${MANDIR}/man1)

# Miscellaneous extras:
ADD_SUBDIRECTORY("doxygen")
